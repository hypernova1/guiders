plugins {
    id 'java'
    id 'war'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group = 'org.brokers'
version = '1.0.0-BUILD-SNAPSHOT'
description = 'guiders'
java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework:spring-webmvc:5.3.3'

    implementation 'org.springframework:spring-aspects:5.3.3'
    implementation 'org.aspectj:aspectjrt:1.9.6'
    implementation 'org.aspectj:aspectjweaver:1.9.5'

    implementation 'org.springframework:spring-websocket:5.3.3'
    implementation 'org.springframework:spring-messaging:5.3.3'

    implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.0.RELEASE'

    implementation 'org.springframework.data:spring-data-jpa:2.4.3'
    implementation 'org.hibernate:hibernate-envers:5.4.27.Final'
    implementation 'com.querydsl:querydsl-jpa:4.4.0'

    implementation 'org.mariadb.jdbc:mariadb-java-client:2.3.0'
    implementation 'com.zaxxer:HikariCP:4.0.2'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'
    implementation 'org.thymeleaf:thymeleaf-spring5:3.0.11.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.5.2'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.1'

    implementation 'commons-fileupload:commons-fileupload:1.3.3'
    implementation 'org.apache.commons:commons-lang3:3.11'

    implementation 'org.modelmapper:modelmapper:2.3.9'

    implementation 'org.imgscalr:imgscalr-lib:4.2'

    implementation 'com.github.scribejava:scribejava-core:2.8.1'

    implementation 'org.apache.logging.log4j:log4j-core:2.11.2'
    implementation 'org.slf4j:slf4j-log4j12:1.7.30'

    implementation 'javax.servlet:javax.servlet-api:4.0.1'

    testImplementation 'org.springframework:spring-test:5.3.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main {
        java {
            srcDir querydslDir
        }
        resources {
            srcDirs "src/main/resources", "src/main/resources/${profile}"
        }
    }
}

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}


war {
    archiveName 'ROOT.war'
    from 'webapp'
}